apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../config/quality.gradle'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.1"

  defaultConfig {
    applicationId "com.pwittchen.search.twitter"
    minSdkVersion 19
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      buildConfigField("String", "TWITTER_CONSUMER_KEY", "\"" + TWITTER_CONSUMER_KEY + "\"")
      buildConfigField("String", "TWITTER_CONSUMER_SECRET", "\"" + TWITTER_CONSUMER_SECRET + "\"")
      buildConfigField("String", "TWITTER_ACCESS_TOKEN", "\"" + TWITTER_ACCESS_TOKEN + "\"")
      buildConfigField("String", "TWITTER_ACCESS_TOKEN_SECRET",
          "\"" + TWITTER_ACCESS_TOKEN_SECRET + "\"")

      minifyEnabled false
      applicationIdSuffix ".debug"
    }

    release {
      buildConfigField("String", "TWITTER_CONSUMER_KEY", "\"" + TWITTER_CONSUMER_KEY + "\"")
      buildConfigField("String", "TWITTER_CONSUMER_SECRET", "\"" + TWITTER_CONSUMER_SECRET + "\"")
      buildConfigField("String", "TWITTER_ACCESS_TOKEN", "\"" + TWITTER_ACCESS_TOKEN + "\"")
      buildConfigField("String", "TWITTER_ACCESS_TOKEN_SECRET",
          "\"" + TWITTER_ACCESS_TOKEN_SECRET + "\"")

      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  packagingOptions {
    exclude 'META-INF/DEPENDENCIES.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/notice.txt'
    exclude 'META-INF/license.txt'
    exclude 'META-INF/dependencies.txt'
    exclude 'META-INF/LGPL2.1'
    exclude 'LICENSE.txt'
    exclude 'ASL-2.0.txt'
    exclude 'LGPL-3.0.txt'
    exclude 'META-INF/ASL-2.0.txt'
    exclude 'META-INF/LGPL-3.0.txt'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
    exclude 'META-INF/services/com.fasterxml.jackson.databind.Module'
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  // reactive libraries
  compile 'io.reactivex:rxjava:1.0.14'
  compile 'io.reactivex:rxandroid:1.0.1'

  // dependency injection & view injection
  compile 'com.google.dagger:dagger:2.0'
  compile 'com.jakewharton:butterknife:6.1.0'
  apt 'com.google.dagger:dagger-compiler:2.0'
  provided 'javax.annotation:jsr250-api:1.0'

  // backward compatibility and design support
  compile 'com.android.support:appcompat-v7:23.1.0'
  compile 'com.android.support:recyclerview-v7:23.1.0'
  compile 'com.android.support:design:23.1.0'

  // others: twitter, search view, loading images, time operations
  compile 'org.twitter4j:twitter4j-core:4.0.4'
  compile 'com.miguelcatalan:materialsearchview:1.2.0'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'joda-time:joda-time:2.8.2'

  // test dependencies
  androidTestCompile 'junit:junit:4.12'
  androidTestCompile('com.google.truth:truth:0.25') {
    exclude group: 'junit' // Android has JUnit built in
  }
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2' // required by Mockito
  androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2' // required by Mockito
  androidTestCompile 'org.mockito:mockito-core:1.10.19'
  androidTestCompile 'com.android.support.test:runner:0.4.1'
  androidTestCompile 'com.android.support.test:rules:0.4.1'
  androidTestCompile 'com.android.support:support-annotations:23.1.0'
}
